import org.gradle.crypto.checksum.Checksum

plugins {
	id "distribution"
	id "com.github.johnrengelman.shadow" version "5.2.0"
	id "java"
	id "com.github.breadmoirai.github-release" version "2.2.9"
	id "io.toolebox.git-versioner" version "1.3.0"
	id 'org.gradle.crypto.checksum' version '1.1.0'
	id "com.diffplug.gradle.spotless" version "3.26.1"
	id 'de.fuerstenau.buildconfig' version '1.1.8'
}

repositories {
	jcenter()
}

dependencies {
	implementation 'com.offbytwo:docopt:0.6.0.20150202'

	implementation 'org.apache.commons:commons-text:1.8'
	implementation 'info.picocli:picocli:4.2.0'
	implementation 'io.quarkus.qute:qute-core:1.1.0.Final'
	implementation 'kr.motd.maven:os-maven-plugin:1.6.1'
	implementation 'org.codehaus.plexus:plexus-java:1.0.4'

	implementation 'com.google.code.gson:gson:2.8.6'

	implementation 'org.jsoup:jsoup:1.13.1'

	implementation 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-api:3.1.4'
	implementation 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-maven:3.1.4'
	implementation "org.slf4j:slf4j-nop:1.7.25"
	//implementation 'org.apache.maven:maven-aether-provider:3.0.5'
	//implementation 'com.google.guava:guava:28.2-jre'

	testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.5.2')
	testImplementation "com.github.stefanbirkner:system-rules:1.17.2"
	testImplementation(
			'org.hamcrest:hamcrest-library:2.1'
	)
}

// to enable reproducible builds
tasks.withType(AbstractArchiveTask) {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}

spotless {
	format 'misc', {
		target '**/*.gradle', '**/*.md', '**/.gitignore'
		targetExclude 'CONTRIBUTORS.md' // all-contributor bot adds non-indented code
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		importOrder 'java', 'javax', 'org', 'com', 'dk.xam', ''
		removeUnusedImports()
		eclipse().configFile "misc/eclipse_formatting_nowrap.xml"
	}
	format 'xml', {
		target '**/*.xml', '**/*.nuspec'
	}
}

distributions {
	main {
		contents {
			from('examples') {
				include '**/*.java'
				into 'examples'
			}
			from(shadowJar) {
				into 'bin'
			}
			from('src/main/scripts') {
				include 'jbang'
				include 'jbang.bat'
				into 'bin'
			}
		}
	}
}

jar {
	manifest {
		attributes (
				'Main-Class': 'dk.xam.jbang.Main',
		)
	}
	}


compileJava {
	options.encoding = 'UTF-8'
	options.compilerArgs << "-Xlint:unchecked"
}

compileTestJava {
	options.encoding = 'UTF-8'
	options.compilerArgs << "-Xlint:unchecked"
}


shadowJar {
	minimize()  {
		exclude(dependency('org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-spi:.*'))
		exclude(dependency('org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-maven:.*'))
		//exclude(dependency('org.slf4j:slf4j-api:.*'))
		exclude(dependency('org.slf4j:slf4j-nop:.*'))


	}
	mergeServiceFiles()
	manifest {
		attributes 'Main-Class': 'dk.xam.jbang.Main'
	}
	// warning is printed about deprecation in 7.0 but if I fix this shadowjar turns into a bogus file
	archiveName = "${baseName}.${extension}"
}

test {
	useJUnitPlatform()
}

task copyResources(type: Copy) {
	from "${projectDir}/src/main/resources"
	into "${buildDir}/classes/java/main"
}
processResources.dependsOn copyResources


task copyTestResources(type: Copy) {
	from "${projectDir}/examples"
	into "${buildDir}/classes/java/test/examples"
}
processTestResources.dependsOn copyTestResources

task createChecksum(type: Checksum) {
	dependsOn(assembleDist)
	files = distZip.outputs.files
	outputDir = distZip.destinationDir
	algorithm = Checksum.Algorithm.SHA256
}

task homebrew(type: Copy) {
	dependsOn(createChecksum)
	inputs.property('version', project.version)
	from('src/main/scripts/brew/jbang.rb')
	into(buildDir.toString() + '/brew/formula')
	doFirst { t ->
		t.filter(org.apache.tools.ant.filters.ReplaceTokens,
			tokens: [
				projectVersion: project.version,
				sha256        : new File(distZip.archiveFile.asFile.get().absolutePath + '.sha256').text
			]
		)
	}
}

task container(type: Copy) {
	dependsOn(createChecksum)
	inputs.property('version', project.version)
	from('src/main/scripts/container/Dockerfile')
	from('src/main/scripts/container/README.md')
	into(buildDir.toString() + '/container')
	doFirst { t ->
		t.filter(org.apache.tools.ant.filters.ReplaceTokens,
				tokens: [
						projectVersion: project.version,
						sha256        : new File(distZip.archiveFile.asFile.get().absolutePath + '.sha256').text
				]
		)
	}
}

task spec(type: Copy) {
	dependsOn(createChecksum)
	inputs.property('version', project.version)
	from('src/main/scripts/spec/jbang.spec')
	into(buildDir.toString() + '/spec')
	doFirst { t ->
		t.filter(org.apache.tools.ant.filters.ReplaceTokens,
				tokens: [
						projectVersion: project.version,
						sha256        : new File(distZip.archiveFile.asFile.get().absolutePath + '.sha256').text
				]
		)
	}
}

task scoop(type: Copy) {
	dependsOn(createChecksum)
	inputs.property('version', project.version)
	from('src/main/scripts/scoop/jbang.json')
	into(buildDir.toString() + '/scoop')
	doFirst { t ->
		t.filter(org.apache.tools.ant.filters.ReplaceTokens,
				tokens: [
						projectVersion: project.version,
						sha256        : new File(distZip.archiveFile.asFile.get().absolutePath + '.sha256').text
				]
		)
	}
}

task chocolatey(type: Copy) {
	dependsOn(createChecksum)
	inputs.property('version', project.version)
	from('src/main/scripts/choco')
	into(buildDir.toString() + '/choco')
	doFirst { t ->
		t.filter(org.apache.tools.ant.filters.ReplaceTokens,
				tokens: [
						projectVersion: project.version,
						sha256        : new File(distZip.archiveFile.asFile.get().absolutePath + '.sha256').text
				]
		)
	}
}

task copyITests(type: Copy) {
	from "${projectDir}/itests"
	into "${buildDir}/itests"
}

githubRelease {
	// currently does not work as project.version not set.
	// FilenameFilter filter = { dir, filename -> filename.contains(project.version) } // ensure nothing extra gets included.
	//releaseAssets = distZip.destinationDir.listFiles filter
	releaseAssets = distZip.destinationDir.listFiles()
	//println(getReleaseAssets().getFiles())
	// set ORG_GRADLE_PROJECT_github_token
	token = project.hasProperty('github_token') ? getProperty('github_token') : "unknown_github_token"
	owner = "jbangdev"
	draft = true
	body = "## News"
}

versioner {
	startFrom {
		major = 0
		minor = 0
		patch = 1
	}
	git {
		authentication {
			https {
				token = project.hasProperty('github_token') ? getProperty('github_token') : "unknown_github_token"
			}
		}
	}
}

build.dependsOn(copyITests)

build.dependsOn(homebrew)
build.dependsOn(chocolatey)
build.dependsOn(scoop)
build.dependsOn(container)
build.dependsOn(spec)

group = "dk.xam.jbang"
sourceCompatibility = '8'
targetCompatibility = '8'
